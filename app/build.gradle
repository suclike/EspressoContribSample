/*
 * Copyright 2015 yuki312 All Right Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "yuki.m.android.espressocontribsample"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
    configurations {
        // Resolved 'com.android.support:support-annotations' versions for app (22.1.1) and test app (22.0.0) differ.
        // Add this statement if 'com.android.support:support-annotations:x.x.x' dependencies was already defined in compile scheme.
        androidTestCompile.exclude group: 'com.android.support', module: 'support-annotations'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // 'appcompat-v7' has 'support-annotations'.
    compile 'com.android.support:appcompat-v7:22.1.1'

    compile 'com.android.support:recyclerview-v7:21.0.3'

    // You can compile without 'espresso-core'. Because 'espresso-contrib' has.
    // androidTestCompile 'com.android.support.test.espresso:espresso-core:2.1'

    // When you use "espresso-contrib:2.0" with "rules:0.2".
    // might "com.android.dex.DexException: Multiple dex files define Landroid/support/test/BuildConfig;" occur.
    androidTestCompile ('com.android.support.test.espresso:espresso-contrib:2.1') {
        // Resolved java.lang.NoClassDefFoundError: your.package.name.EspressoTargetActivity
        // 'espresso-contrib' has already 'support-v4' package.
        exclude group: 'com.android.support', module: 'support-v4'

        // Resolved java.lang.IllegalAccessError: Class ref in pre-verified class resolved to unexpected implementation
        // 'espresso-contrib' has already 'recyclerview-v7' package.
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

    // You can compile without 'runner'. Because 'rules' has.
    // androidTestCompile 'com.android.support.test:runner:0.2'
    androidTestCompile 'com.android.support.test:rules:0.2'
}
